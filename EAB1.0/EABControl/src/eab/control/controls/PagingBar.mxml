<?xml version="1.0" encoding="utf-8"?>
<mx:HBox 
	xmlns:mx="http://www.adobe.com/2006/mxml" 
	borderStyle="none"
	width="100%" 
	height="100%">
	<mx:Metadata>
        [Event(name="pageChanged", type="eab.control.events.PageChangeEvent")]
    </mx:Metadata>
	<mx:Script>
		<![CDATA[
			import eab.control.events.PageChangeEvent;
			import mx.binding.utils.BindingUtils;
			
			[Bindable]
			private var _showSummary : Boolean = true;
			
			[Bindable]
			private var _summaryText : String;
		
			[Bindable]
			private var _currentPage:Number = 1;
			
			[Bindable]
			private var _totalCount:Number = 0;
			
			[Bindable]
			private var _countPerPage:Number = 20;
			
			[Bindable]
			private var _pageArray:Array = new Array();
			
			[Bindable]
			private var _totalPages:Number = 1;
			
			public function set currentPage(page:Number):void{
				if(_currentPage == page)
					return;
				
				if(page < 1 || page > _totalPages)
					return;
					
				_currentPage = page;
				
				pageSelecter.selectedIndex = _currentPage - 1;
				getSummaryString();
				this.dispatchEvent(new PageChangeEvent(_currentPage, _countPerPage, "pageChanged"));
			}
			
			public function get currentPage():Number{
				return _currentPage;
			}
			
			public function set countPerPage(count:Number):void{
				if(count == _countPerPage || count <= 0)
					return;
				
				_countPerPage = count;
				countPageArray();
				getSummaryString();
			}
			
			public function get countPerPage():Number{
				return _countPerPage;
			}
			
			public function set totalCount(total:Number):void{
				if(total == _totalCount || total <= 0)
					return;
				
				_totalCount = total;
				countPageArray();
				getSummaryString();
			}
			
			public function get totalCount():Number{
				return _totalCount;
			}
			
			public function get totalPages():Number{
				return _totalPages;
			}
			
			public function get showSummary():Boolean{
				return _showSummary;
			}
			
			public function set showSummary(isShow : Boolean):void{
				_showSummary = isShow;
			}
			
			private function countPageArray():void{
				if(_totalCount == 0)
					_totalPages = 1;
					
				_totalPages = Math.ceil(_totalCount / _countPerPage);
				_pageArray = new Array();
				for(var i:Number = 1; i <= _totalPages; i++){
					_pageArray.push(i);
				}
				
				this.pageSelecter.selectedIndex = _currentPage - 1;
			}			
			
			private function getSummaryString():void{
				if(!_showSummary)
					_summaryText = null;
				else			
					_summaryText = "共" + _totalCount + "条记录，当前(" 
					+ _currentPage  + "/"  + _totalPages + ")第" + 
					((_currentPage-1) * _countPerPage + 1) + "-" + 
					((_currentPage * _countPerPage > _totalCount) ? _totalCount : _currentPage * _countPerPage) + 
					"条";
			}
		]]>
	</mx:Script>
	<mx:HBox id="pagingControl" width="100%" height="100%" visible="{_totalPages > 1}" borderStyle="none" horizontalGap="-1">
		<mx:LinkButton id="goStart" label="首页" click="currentPage = 1"/>
		<mx:LinkButton id="goPrevious" label="上一页" click="if(currentPage > 1)currentPage = currentPage - 1"/>				   
		<mx:ComboBox id="pageSelecter" textAlign="center" dataProvider="{_pageArray}" editable="false" change="currentPage = new Number(this.pageSelecter.selectedLabel)"/>
		<mx:LinkButton id="goNext" label="下一页" click="if(_totalPages > currentPage) currentPage = currentPage + 1"/>				   
		<mx:LinkButton id="goEnd" label="末页" click="currentPage=_totalPages"/>
	</mx:HBox>
	<mx:Label  visible="{_showSummary}" textAlign="right" width="100%" height="100%" id="nowState" 
		text="{_summaryText}"/>
</mx:HBox>
